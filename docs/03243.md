# 列表

> 原文：<https://dev.to/gillchristian/day-16-lists-3d8k>

> 思维的外显教学不是一件小事，但谁说编程的教学是呢？用我们的术语来说，越明确地教授思维，程序员就越会成为科学家。
> *埃德格·迪杰斯特拉*

更多信息请见™️，这次是上周的会议，[代尔夫特哈斯克尔研究小组#6](https://www.meetup.com/Delft-Haskell-Study-Group/events/260075544/) 。和往常一样，我引用《T3》这本书中的[作为总结📖](http://haskellbook.com/)

## 关于思考🤔

我总是在每一章的封面上添加书中的引语，但从不提及任何关于它们的内容。我真的特别喜欢这个。编程就是思考。我们越是学会思考，我们就会成为越好的程序员。

## 列表

Haskell 中的列表数据类型是这样定义的:

```
data [] a = [] | a : [a] 
```

有几件事需要指出。`[]`既是空列表的类型构造函数，也是空列表的数据构造函数。另一个构造函数(`:`)，名为 [*cons*](https://en.wikipedia.org/wiki/Cons) 的中缀运算符，接受一个`a`类型的值和一个`[a]`类型的列表，计算结果为`[a]`。数据类型总体上是一个 sum ( `|`)，但是第二个构造函数(`:`)是一个乘积。`a : [a]`所做的是将`a`添加到列表的前面。

### 模式匹配🎉

正如我已经提到的，模式匹配是我最喜欢的 Haskell 特性之一。通过对列表进行模式匹配，我们可以做一些有趣的事情。

```
head' :: [a] -> Maybe a
head' []       = Nothing
head' (x : _ ) = Just x

tail' :: [a] -> Maybe [a]
tail' []       = Nothing
tail' (_ : xs) = Just xs 
```

我编写了这些版本来返回包装在 maybe 中的值，因为`[a]`不保证列表会有值。如果我们在空列表上尝试这些函数的 [Prelude](http://hackage.haskell.org/package/base-4.12.0.0/docs/Prelude.html) 版本，我们会得到错误。

```
λ> head []
*** Exception: Prelude.head: empty list
λ> tail []
*** Exception: Prelude.tail: empty list 
```

关于我们的`tail'`，有一点要提一下。如果用只有一个值的列表调用它，它将返回`Just []`。我认为这很好，因为它反映了只有一个元素的列表的尾部是什么(`a : []`)。但是如果我们只想返回包含元素的列表，我们可以这样扩展它。

```
tail' :: [a] -> Maybe [a]
tail' []       = Nothing
tail' (_ : []) = Nothing
tail' (_ : xs) = Just xs 
```

### 甜甜甜甜的句法糖🍭

要使用构造函数编写一个包含四个元素的列表，我们必须使用 cons 操作符 4 次。

```
(1 : 2 : 3 : 4 : []) 
```

幸运的是，Haskell 提供了一些语法糖来使它更容易，这类似于许多其他语言中的语法糖。

```
[1, 2, 3, 4] 
```

### 范围

Haskell 提供的另一个处理列表的好东西是 ranges 语法。

```
λ> [1..10]
[1,2,3,4,5,6,7,8,9,10]

λ> [2, 4..10]
[2,4,6,8,10]

λ> [1..]
[1,2,3,4,5,6,7,8,9,10,11,12 ...] -- continues infinitely

λ> [1, 5..]
[1,5,9,13,17,21,25,29,33,37,41,45 ...] -- continues infinitely 
```

相当于。

```
λ> enumFromTo 1 10
[1,2,3,4,5,6,7,8,9,10]

λ> enumFromThenTo 2 4 10
[2,4,6,8,10]

λ> enumFrom 1
[1,2,3,4,5,6,7,8,9,10,11,12 ...] -- continues infinitely

λ> enumFromThen 1 5
[1,5,9,13,17,21,25,29,33,37,41,45 ...] -- continues infinitely 
```

所有这些函数都需要被定域的类型，换句话说就是一个`Enum`类型类。

```
enumFromTo :: Enum a => a -> a -> [a]
enumFromThenTo :: Enum a => a -> a -> a -> [a]

enumFrom :: Enum a => a -> [a]
enumFromThen :: Enum a => a -> a -> [a] 
```

## 列举理解

> 列表综合是一种从一个列表或多个列表生成新列表的方法。它们直接来自数学中的集合理解概念，包括类似的句法。它们必须至少有一个列表，称为生成器，为理解提供输入，也就是说，提供一组项目，新列表将从这些项目中构建。它们可能具有确定从列表中提取哪些元素的条件和/或应用于这些元素的功能。

来自非数学背景(JS，PHP，C++，Go，。这对我来说是一件非常新鲜的事情。

```
[ x^2 | x <- [1..10]] 
```

给出定义我们可以猜测我们正在从`[1..10]`的列表中构建一个所有平方数的列表。或者至少我是这么猜测的，因为我已经读过这一章了😂

```
λ> [ x^2 | x <- [1..10]]
[1,4,9,16,25,36,49,64,81,100] 
```

我们可以添加谓词来忽略某些项目。当然是谓词返回`False`的那些。例如，我们可以决定只保留能被 3 整除的数字。

```
λ> [x^2 | x <- [1..10], rem x 3 == 0]
[9,36,81] 
```

列表理解可以有多个生成器。

```
λ> [x^y | x <- [1..5], y <- [2, 3]]
[1,1,4,8,9,27,16,64,25,125]

λ> [(x, y) | x <- [1..5], y <- [2, 3]]
[(1,2),(1,3),(2,2),(2,3),(3,2),(3,3),(4,2),(4,3),(5,2),(5,3)] 
```

请注意，最右边的发生器首先耗尽，然后是第二个，依此类推。

### 棘而不紧的评价

列表是由空列表`[]`终止的 cons 单元格`a : [a]`的递归序列。现在，找到一种可视化的方法会很棒。

在 Haskell 中，我们说一些数据结构(列表、序列和树)有一个*主干*，这个连接结构将值的集合联系在一起。在列表中是递归的 cons ( `:`)操作符。

```
1 : 2 : 3 : []

or

1 : (2 : (3 : [])) 
```

这个`1 : 2 : 3 : []`表示有一个问题，它看起来像是在*单元格*之前就有了`1`，但实际上它包含了两个值。看起来更像下面的。

```
 :
 / \
1   :
   / \
  2   :
     / \
    3  [] 
```

> 由于这一点以及非严格评估的工作方式，您可以独立于 cons 单元格所包含的内容来评估它们。可以只评估列表的主干而不评估单个值。也有可能只评估列表主干的一部分，而不评估其余部分。

对一个列表的评估沿着脊柱向下，而构造则向上。但是直到需要的时候才进行评估。

我们说过，脊柱是 cons 运算符(`:`)的递归序列。在这里，`_`将是未评估的值。

```
 : <------|
/ \       |
_  : <----| This is the "spine"
  / \     |
 _   : <--|
    / \
   _  [] 
```

在 GHCi 中使用`:sprint`,我们可以看到哪些被评估，哪些没有被评估。

```
λ> ls = [1..5] :: [Int]
λ> :sprint ls
ls = _ 
```

所以，`ls`还没有被评估(如`_`所示)。

```
λ> take 1 ls
[1]
λ> :sprint ls
ls = 1 : _ 
```

现在我们评估第一个 cons 单元格及其第一个值。我们取 2 个值。

```
λ> take 2 ls
[1, 2]
λ> :sprint ls
ls = 1 : 2 : _ 
```

我们可以一直这样下去，直到最后。我们也可以用`length`来评价整个脊柱。

```
λ> length ls
5
λ> :sprint ls
ls = [1,2,3,4,5] 
```

等等，我说的是只有脊椎🤔这是 GHCi 的一个怪癖。但是我们可以通过检查一列底部⊥(即`undefined`)来证明这只是脊椎。

```
λ> undefined
*** Exception: Prelude.undefined
CallStack (from HasCallStack):
  error, called at libraries/base/GHC/Err.hs:78:14 in base:GHC.Err
  undefined, called at <interactive>:1:1 in interactive:Ghci1 
```

正如我们所见，它在求值时抛出了一个异常。但是...

```
λ> ls = [undefined, undefined]
λ> :sprint ls
ls = _
λ> length ls
2
λ> :sprint ls
ls = [_,_] 
```

正如我们所看到的，Haskell 只评估它需要的东西，其他什么都不做。这本书对此做了更多的扩展，解释了*范式*和*弱头范式*的概念。但是我会在非严格性一章中更深入地讨论这些问题。

### 转换值列表

我们可以使用`map`或`fmap`来转换值列表，方法是对每个元素应用一个函数，然后返回新的列表。

```
map  ::              (a -> b) -> [a] -> [b]
fmap :: Functor f => (a -> b) -> f a -> f b 
```

`fmap`是`map`的一般化版本，适用于函子。名单就是其中之一。在下一章中会有更多关于函子的内容。

```
λ> fmap (+1) [1, 2, 3, 4]
[2,3,4,5] 
```

这是它在`base`中的定义。

```
map :: (a -> b) -> [a] -> [b]
map _ []     = []
map f (a:as) = f a : map f as 
```

通过在类型定义中添加一些参数，我们可以更清楚地知道 map 真正允许我们做什么。基本上提升了我们的函数`a -> b`来处理列表。

```
(a -> b) -> ([a] -> [b])

( a  ->  b )
([a] -> [b]) 
```

### 过滤值列表

不是我们可以在列表上使用的唯一函数。当某些值不满足谓词时，我们也可以从列表中过滤掉它们。

```
λ> even 10
True
λ> even 9
False
λ> filter even [1..10]
[2,4,6,8,10] 
```

下面是`filter`的定义。

```
filter :: (a -> Bool) -> [a] -> [a]
filter _ [] = []
filter pred (x:xs)
  | pred x    = x : filter pred xs
  | otherwise = filter pred xs 
```

### 压缩列表

> 将列表压缩在一起是一种将多个列表中的值合并到一个列表中的方法。

```
λ> :t zip
zip :: [a] -> [b] -> [(a, b)]
λ> zip "abc" "cde"
[('a','c'),('b','d'),('c','e')]
λ> zip "abc" "cdefghi"
[('a','c'),('b','d'),('c','e')]
λ> zip "abcdefghi" "jkl"
[('a','j'),('b','k'),('c','l')] 
```

注意`zip`返回 shortests 列表的长度。

我们也可以`unzip`列举。

```
λ> :t unzip
unzip :: [(a, b)] -> ([a], [b])
λ> unzip [(1,'a'), (2, 'b'), (3, 'c')]
([1,2,3],"abc") 
```

还有`zipWith`来合并两个列表。

```
λ> :t zipWith
zipWith :: (a -> b -> c) -> [a] -> [b] -> [c]
λ> zipWith (+) [1,2,3] [1,2,3]
[2,4,6]
λ> zipWith (+) [1,2,3] [1,2,3]
[2,4,6]
λ> zipWith (,) [1,2,3] [1,2,3]
[(1,1),(2,2),(3,3)] 
```

## 结论

有了列表，我们开始看到函数式编程的声明性模式。以及我们可以用于普通值的相同函数如何可以用于列表以及像`map`、`filter`和`zip`这样的高阶函数。

由于 Haskell 的懒惰(非严格评估),它只会评估它所需要的，不会更多。

```
λ> ls = [1,2,3,4,5,6,7,undefined]
λ> filter even ls
[2,4,6*** Exception: Prelude.undefined
λ> take 2 $ zip "abcdefg" $ map (*2) $ filter even ls
[('a',4),('b',8)] 
```

虽然在这个例子中很明显，但它有更多的含义，我还没有理解甚至发现，我期待着了解这一点。