# Elixir 中的哈希文件

> 原文：<https://dev.to/alvisesus/hashing-files-in-elixir-2pbb>

你对仙丹和凤凰有热情吗？订阅 Poeticoding 时事通讯，加入快乐的普通读者，通过电子邮件接收新帖子。

**散列函数**是转换可变大小的字节序列(字符串、文件内容等)的函数。)到固定大小的字节序列，称为**摘要**。这意味着散列任何长度的文件，散列函数将总是为该文件返回相同的唯一字节序列。这是一种数字指纹，通常由长度在 32 到 128 个字符之间的十六进制字符串表示。

文件的散列是有用的，例如，了解两个文件的内容是否相同，或者内容是否在下载期间被损坏。

有不同的散列函数，MD5，SHA-1，SHA-2，SHA-3 等。他们中的许多人都有长生不老药。

**更新**👨‍💻:我最初使用 MD5 算法(这是列表中最弱的算法)编写下面的示例，只是因为我认为它是最快的算法。 [@Hauleth](https://twitter.com/Hauleth) 指出，由于[英特尔 SHA 扩展](https://en.wikipedia.org/wiki/Intel_SHA_extensions)，SHA-1 和 SHA-256 在新的 CPU 上应该更快，所以我用 SHA-256 重写了例子

# 散列字符串

让我们开始使用 SHA-256 算法散列一个字符串。

```
iex> :crypto.hash(:sha256,"I love Elixir")
<<164, 35, 167, 235, 69, 224, 253, 77, 180, 92, 77, 172, 37,...>>
iex> :crypto.hash(:sha256,"I love Elixir!")
<<209, 119, 188, 230, 168, 124, 98, 212, 119, ...>> 
```

我们使用了 [`:crypto`](http://erlang.org/doc/man/crypto.html) Erlang 模块中的 [`hash/2`函数](http://erlang.org/doc/man/crypto.html#hash-2)。

第一个参数是我们想要使用的散列算法的名称，在本例中是`:sha256`，第二个参数是我们想要散列的字节序列，在本例中是一个字符串。它返回一个字节序列。

我们通过添加一个“！”就可以看到输出是如何变化的性格。

我们可以使用`Base.encode16/1`来获得十六进制的字符串表示

```
iex> :crypto.hash(:sha256,"I love Elixir!") \
...> |> Base.encode16() \
...> |> String.downcase()
"d177bce6a87c62d4772f404fcad2f8c2d9606c04f99942b71d7c521eb79c4c3b" 
```

如果你在 Linux 或 Mac 机器上，你可以使用类似于`sha256sum`的命令行工具来查看摘要是否对应于

```
$ echo -n 'I love Elixir!' |  sha256sum d177bce6a87c62d4772f404fcad2f8c2d9606c04f99942b71d7c521eb79c4c3b - 
```

# 散列一个文件

计算文件的哈希在概念上与计算字符串的哈希相同。文件是一个字节序列，我们可以使用相同的`:crypto.hash(:sha256, file_content_binary)`函数。[但是我们看到大多数时候将整个文件加载到内存中并不是一个好主意！](https://www.poeticoding.com/processing-large-csv-files-with-elixir-streams/)

我们可以使用 [`File.stream!`](https://hexdocs.pm/elixir/File.html#stream!/3) 和 [`:crypto`](http://erlang.org/doc/man/crypto.html#hash_init-1) 中提供的一组不同的函数来成块地读取和处理文件。
让我们先来看一个简单的例子，它使用了我们之前用过的相同的字符串，被分成了几个块

```
iex> [chunk_1, chunk_2] = ["I love ", "Elixir!"]
iex> hash_ref = :crypto.hash_init(:sha256)
#Reference<...36636>
iex> hash_ref = :crypto.hash_update(hash_ref, chunk_1)
#Reference<...36647>
iex> hash_ref = :crypto.hash_update(hash_ref, chunk_2)
#Reference<...36655>
iex> digest = :crypto.hash_final(hash_ref)
<<209, 119, 188, 230, 168, 124, 98, 212, 119, ...>>
iex> digest |> Base.encode16() |> String.downcase()
"d177bce6a87c62d4772f404fcad2f8c2d9606c04f99942b71d7c521eb79c4c3b" 
```

我们分块处理序列，得到与之前相同的结果。

```
hash_ref = :crypto.hash_init(:sha256)

File.stream!(file_path)
|> Enum.reduce(hash_ref, fn chunk, prev_ref-> 
    new_ref = :crypto.hash_update(prev_ref, chunk)
    new_ref
end)
|> :crypto.hash_final()
|> Base.encode16()
|> String.downcase() 
```

*   我们从`:crypto.hash_init(:sha256)`得到一个哈希引用，作为第一个累加器传递给`Enum.reduce`。
*   我们使用`Enum.reduce`从文件中读取每个块，并将其添加到计算中。`:crypto.hash_update/2`返回一个新的参考值，然后将其设置为新的累加器。
*   一旦处理完所有的块，最后的引用就通过管道进入`:crypto.hash_final/1`函数，该函数返回文件的 SHA-256 摘要。

我们可以用更好更简洁的方式编写 reduce 函数

```
File.stream!(file_path)
|> Enum.reduce(:crypto.hash_init(:sha256),&(:crypto.hash_update(&2, &1)))
|> :crypto.hash_final()
|> Base.encode16()
|> String.downcase() 
```

# `File.stream!`大块 vs 线条

默认情况下,`File.stream!`发出行而不仅仅是块。发出行比发出块慢，我想是因为流需要在将块分割成字符串时寻找换行符。

为了强制流发出块，我们使用 [`File.stream!/3`](https://hexdocs.pm/elixir/File.html#stream!/3)

```
iex> File.stream!(file_path, [], 2_048)
%File.Stream{
    line_or_bytes: 2048,
    modes: [:raw, :read_ahead, :binary],
    path: file_path,
    raw: true
} 
```

设置 2048 字节的块大小。

我做了一个快速的基准测试([你可以在这个要点上找到](https://gist.github.com/alvises/54451c50ac0b9bafe0c932e4b2240b32))，我们看到流数据块速度更快，内存方面也更好。

```
Name             ips        average  deviation         median         99th %
chunks       23.29 K       42.93 μs    ±63.44%       41.98 μs       83.98 μs
lines         9.21 K      108.54 μs    ±42.52%       93.98 μs      275.98 μs

Comparison:
chunks       23.29 K
lines         9.21 K - 2.53x slower +65.61 μs

Memory usage statistics:

Name      Memory usage
chunks         2.11 KB
lines         20.84 KB - 9.88x memory usage +18.73 KB 
```

# 总结起来

我们已经了解了什么是散列函数，以及如何使用 Elixir 轻松计算文件的散列。

**过去**(不幸的是我认为还在现在😅)，哈希函数**被**用来在数据库中存储密码。如果您需要**安全**处理和存储密码，请使用 [**bcrypt_elixir**](https://hex.pm/packages/bcrypt_elixir) 库！

你对仙丹和凤凰有热情吗？订阅 Poeticoding 时事通讯，加入快乐的普通读者，通过电子邮件接收新帖子。