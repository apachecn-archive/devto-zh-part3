# 凤凰伞应用程序中的路由

> 原文：<https://dev.to/appsignal/routing-in-phoenix-umbrella-apps-3g0j>

伞状应用程序是构建灵丹妙药项目的绝佳方式。

在窗帘后面，它们是一个非常薄的层，只是将所有东西编译成一个包。你可以用多个独立的上下文来构建你的代码，而不是构建一个单一的整体。它们都在同一个 BEAM 实例下编译和运行，因此它们仍然可以相互访问。与此同时，概念上的分离确保了你为你的每一个保护伞下的孩子拥有独立的 OTP 应用。它允许你在一定程度的隔离下处理每一个问题。

把这个当做穷人的微服务解决方案。您不需要在每个服务之间添加消息队列或发送 HTTP 请求，因为它们实际上都运行在相同的进程下，但是您仍然可以获得一些好处。

如果你想了解更多关于伞式应用的信息，我建议从官方指南开始，因为它清楚地概述了伞式应用的优点和注意事项。

现在让我们看一个真实的例子，我已经实现了一个雨伞应用程序。

## 真实的例子

假设我正在为[魔法:聚会(MTG)](https://en.wikipedia.org/wiki/Magic:_The_Gathering) 卡片建立一个网站。哪个……嗯，[我是](https://github.com/naps62/mse)。这个想法是创建一个用户可以浏览和搜索卡片数据库的界面。还有一个管理面板，在那里可以执行一些管理任务。

显然，每个前端接口都有不同的要求:

1.  主前端是公共的，而管理方面只有私人访问。
2.  管理面板甚至可能有自己的用户界面需求。在这种情况下，为了方便起见，我使用 [ex_admin](https://github.com/smpallen99/ex_admin) 。这意味着，甚至 UI 资产也不共享。
3.  它们大多也有完全不同的后端逻辑。两者之间只能共享一小部分查询和操作。
4.  我可能还想通过不同的网址访问它们(例如，使用一个`admin`子域的管理前端)。

两者之间的诸多差异清楚地表明，这两个应用程序最好是两个独立的 phoenix 应用程序，每个都有自己的设置。

大概是这样:

```
apps/
  client/
  admin/
  shared/ 
```

## 看起来很容易。有什么问题？

当您试图弄清楚如何实际实现这一点时，问题就来了。如何将来自`admin`子域的请求路由到另一个 Phoenix 应用程序，同时将其他请求路由到主 Phoenix 应用程序？

一种解决方案是在不同的端口上运行这些应用程序。但是，这样一来，您要么只能访问`admin.mydomain.com:4001`，要么需要其他中间层从浏览器中抽象出端口区别。虽然这对于只有您可以访问的管理页面来说可能没什么问题，但是对于一般的解决方案来说就不太好了。

传统的解决方案是在客户端和服务器之间放置一个反向代理。nginx 很好地完成了这项工作。但在现实中，你知道所有这一切都是一个单一的药剂应用。需要一个第三方服务器来将请求路由到 It 的不同部分似乎很奇怪。

它也没有解决本地开发的问题，除非您也想在本地运行 nginx，这不太理想。

毕竟我们是长生不老药的开发者，而且我们相当聪明。所以让我们用长生不老药的方式来做:

## 介绍代理 App

我想到的解决方案(例如，从类似的堆栈溢出用例中读取建议)是创建一个额外的伞子，这将是与外界的主要联系点。

这个我们称之为`proxy`的应用程序将接收所有传入的 HTTP 请求，并根据一些简单的规则将它们转发给适当的 Phoenix 应用程序。在我们的简单用例中，对`admin.mydomain.com`的请求将被转发到`admin`应用，所有其他请求将被转发到`client`应用。

这是一个非常简单的 phoenix 应用程序，你可以像其他程序一样用`mix phx.new`来生成它。在这里，依赖性将保持在最低限度。我们只有凤凰&牛仔作为外部依赖(设置我们的 web 服务器)，以及客户端和管理应用程序，我们将转发请求:

```
def deps do
  [
    {:client, in_umbrella: true},
    {:admin, in_umbrella: true},
    {:phoenix, "~> 1.3.2"},
    {:cowboy, "~> 1.0"}
  ]
end 
```

由于这个应用程序将是实际的 web 服务器，我们应该禁用其他两个中的服务器设置:

```
# apps/client/config/config.exs
config :client, Client.Web.Endpoint, server: false

# apps/admin/config/config.exs
config :admin, Admin.Web.Endpoint, server: false

# apps/proxy/config/config.exs
config :proxy, Proxy.Endpoint, server: true 
```

这确保了只有代理应用程序会监听端口。这不是强制性的，但是它为您省去为每个端口定义不同端口的麻烦(记住:每个端口只允许一个监听器),并确保所有请求实际上都通过代理应用程序——这确实是预期的行为。

离开`server: true`在开发或测试模式下可能是有用的，这取决于您想要如何设置您的环境。

## 设置端点

凤凰 app 的切入点是`Endpoint`模块。在这种情况下，我们将它设置为`Proxy.Endpoint`。因为这个应用程序真的没有其他责任，所以没有必要像凤凰城的惯例一样，把它嵌套在`Web`模块下。

然而，我们可以从 Phoenix 生成器为我们创建的端点模块中去掉大部分东西，最终得到一个非常简单的模块:

```
defmodule Proxy.Endpoint do
  use Phoenix.Endpoint, otp_app: :proxy

  @base_host_regex ~r|\.?mydomain.*$|
  @subdomains %{
    "admin" => Admin.Web.Endpoint,
    "client" => Client.Web.Endpoint
  }
  @default_host Client.Web.Endpoint

  def init(opts), do: opts

  def call(conn, _) do
    with subdomain <- String.replace(conn.host, @base_host_regex, ""),
         endpoint <- Map.get(@subdomains, subdomain, @default_host) do
      endpoint.call(conn, endpoint.init())
    end
  end
end 
```

让我们一步一步来看

```
@base_host_regex ~r|\.?mydomain.*$| 
```

这用于提取每个请求的主机 URL 的子域部分。所以对于`admin.mydomain.com`，我们想要得到字符串`admin`，对于`mydomain.com`，我们将得到一个空字符串(这意味着，我们将把它转发到默认应用程序。稍后将详细介绍)。

请注意，这与`.com`部分并不完全匹配。这是我为了地方发展而做的一个方便的改动。在`mydomain.*`上匹配允许我在本地机器上工作时使用`admin.mydomain.lvh.me`,并且仍然让整个逻辑工作，而不需要进行特定于开发的更改。

如果你不知道`lvh.me`是什么，[这篇文章](https://nickjanetakis.com/blog/ngrok-lvhme-nipio-a-trilogy-for-local-development-and-testing#lvh-me)可能会有帮助(TL；DR:这是一个开发服务，它将其名称解析为`localhost`。

记住上面的正则表达式，接下来的部分应该很容易理解:

```
@subdomains %{
    "admin" => Admin.Web.Endpoint,
    "client" => Client.Web.Endpoint
}
@default_host Client.Web.Endpoint 
```

对于每个子域，我们希望匹配一个特定的 Phoenix 端点，该端点属于我们希望转发请求的应用程序。`@default_host`是我们在子域丢失的情况下使用的(我们上面谈到的空字符串场景)。

```
def call(conn, _) do
    with subdomain <- String.replace(host, @base_host_regex, ""),
         endpoint <- Map.get(@subdomains, subdomain, @default_host) do
      endpoint.call(conn, endpoint.init())
    end
end 
```

当这个端点——实际上只不过是一个[药剂插件](https://hexdocs.pm/plug/readme.html)——被调用时，我们只需从请求主机获取子域，然后从映射中找到匹配的端点(默认为`@default_host`),并对其调用`endpoint.call/2`。这实际上是将呼叫委托给适当的应用程序。

现在`client`和`admin`都只需要担心它们相应的请求和认证。所有与我们可能需要的多个子域&客户端相关的逻辑都在这个应用程序中抽象出来了。

想要一个同伞的新客户？这里补充一下！想要同一个端点响应其他子域吗？这里补充一下！

## 走得更远

通过向我们的保护伞应用程序添加智能路由器，我们现在能够在我们的保护伞应用程序中向不同的应用程序提供不同子域的请求。我第一次在我的一个宠物项目上实现了这个模式，但是后来也在一些生产项目上使用并改进了它。

我们可以更进一步。例如，如果您正在将现有服务从 Ruby 迁移到 Elixir。您可以让这个代理应用程序将所有对 Ruby 版本服务的请求重定向回 Ruby 应用程序，从而确保向后兼容性。或者您可能想要相反的场景，即您正在创建一个新的 API 服务，并且想要将匹配的请求转发到不同的客户机，甚至是不同的 web 服务器。

我们还可以将路由复杂性提升到另一个层次。这里的路由完全基于请求的子域。但是根据您的需要，您可以使用 HTTP 头或查询参数创建更复杂的路由规则。所有这些都可以在实际的 web 服务完全不知道的情况下完成。

我们玩得很开心💥思考伞和路由的所有可能性。我们希望它能让你意识到🔥火也是。如果你想获得常规剂量的⚗️Elixir 炼金术，请[订阅](https://appsignal.com/elixir-alchemy)让下一集炼金术直接发送到你的收件箱。

*本文由客座作者[米盖尔·帕哈斯](http://naps62.github.io/)撰写。米盖尔是专业的总工程师[@子视觉](https://twitter.com/subvisual)，组织 [@rubyconfpt](https://twitter.com/rubyconfpt) 和 [@MirrorConf](https://twitter.com/mirrorconf) 。*