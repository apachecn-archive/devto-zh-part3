# 直到 GenServer 的' handle_continue/2 '

> 原文:# t0]https://dev . to/sophiederebedit/-style 基因服务器-handle net 2-1 cej

*这篇文章最初发表在[仙丹学院博客](https://elixirschool.com/blog)上。仙丹学校是一个开源的仙丹课程，我们正在寻找贡献者！你可以写一篇短的 TIL 博客文章，一篇更长的博客文章，添加课程，帮助翻译等等。查看我们未解决的问题或添加您自己的问题[点击](https://github.com/elixirschool/elixirschool/)*

当启动您的 GenServer 需要执行一个长时间运行的进程时会发生什么？我们*不*希望该进程的执行阻止发电机完成启动。我们也不希望异步执行该流程，从而在流程运行和到达我们的 GenServer 收件箱的其他消息之间产生竞争。在本帖中，我们将仔细研究这两个问题，并理解 OTP 21 的`GenServer.handle_continue/2`如何是完美的解决方案。

## 无阻塞启动发电服务器

假设我们正在为一个购物清单实现应用程序构建一个 GenServer。我们的 GenServer 将保存描述杂货店购物清单的状态*和*,了解与该购物清单相关的可用库存。当我们的 GenServer 启动时，它将接收一个购物清单并将其置于状态。但是等等！我们的 GenServer 初始化过程将*然后*需要获取购物清单，并从另一个来源检索相关库存。

我们解决这个问题的第一次尝试可能是这样的:

```
defmodule ShoppingListFulfillment do
  use GenServer

  def start_link(shopping_list) do
    GenServer.start_link(__MODULE__, shopping_list)
  end

  def init(shopping_list) do
    state = %{
      shopping_list: shopping_list
      inventory: get_inventory_for(shopping_list)
    }
    {:ok, state}
  end

  defp get_inventory_for(shopping_list) do
    # something that could be time consuming!
    # like a web request or a database call
    # returns some inventory info for each item on the shopping list
  end
end 
```

这里，我们在调用`start_link/1`时触发的`init/1`回调中调用我们的“获取购物清单项目的库存”代码。

这种方法的问题是，`start_link/1`将阻塞，直到`init/1`返回`{:ok, state}`。我们不会从`init/1`返回，直到之后的*盘点代码运行。那*可能*会很费时间。我们不希望我们的 GenServer 被这个阻塞。*

让我们探索一种异步方法。

## 异步回调和竞争条件

我们可以在我们的`init`回调中使用`Kernel.send/2`来启动一些异步工作，而不会阻塞`GenServer.start_link/1`。当我们使用`send/2`并给它第一个参数`self`，即我们的 GenServer 的 PID，我们的 GenServer 将使用与我们发送的消息匹配的`handle_info/2`函数处理该消息。

```
defmodule ShoppingListFulfillment do
  use GenServer

  def start_link(shopping_list) do
    GenServer.start_link(__MODULE__, shopping_list)
  end

  def init(shopping_list) do
    state = %{
      shopping_list: shopping_list
      inventory: []
    }
    send(self, :get_inventory)
    {:ok, state}
  end

  def handle_info(:get_inventory, %{shopping_list: shopping_list}) do
    inventory = get_inventory_for(shopping_list)
    state = %{
      shopping_list: shopping_list,
      inventory: inventory
    }
    {:noreply, state}
  end

  defp get_inventory_for(shopping_list) do
    # something that could be time consuming!
    # like a web request or a database call
    # returns some inventory info for each item on the shopping list
  end
end 
```

这种方法解除了`GenServer.start_link/1`的封锁。它不再需要*等待*获取库存的工作。现在，这是异步发生的，一旦我们完成获取库存信息，它就会更新状态。

不过这种方法也有不好的一面。仅仅因为我们在`init`函数中发送`:get_inventory`消息，并不意味着`:get_inventory`是我们的 GenServer 将接收和处理的第一条消息。这可能导致竞态条件！

如果我们的 GenServer 在收到并完成处理消息以获取库存之前*收到一条消息，询问购物清单上某个商品的可用性，会发生什么情况？那会导致假阴性！我们会看到状态中的`inventory`为空，并告诉发送者他们的商品不可用。哦不！*

如果有某种方法可以异步获取库存，而不阻塞`start_link/1`，同时*仍然*确保它在响应我们的 GenServer 收到的任何其他消息之前执行*就好了...*

## 使用`handle_continue/2`

几个月前 OTP 21 的发布给了我们解决这个问题的方法。每当先前的回调返回`{:continue, :message}`时，由 GenServer 进程调用 [`GenServer.handle_continue/2`](https://hexdocs.pm/elixir/GenServer.html#c:handle_continue/2) 回调。

> `handle_continue/2`在前一个回调之后立即被调用，这对于在初始化之后执行工作或者将回调中的工作分成多个步骤，从而在过程中更新流程状态非常有用。 [*](http://erlang.org/doc/man/gen_server.html#Module:handle_continue-2)

这种方法确保了在`handle_continue/2`完成之前，我们的 GenServer 不会处理任何其他消息。没有更多的比赛条件！

让我们来看看:

```
defmodule ShoppingListFulfillment do
  use GenServer

  def start_link(shopping_list) do
    GenServer.start_link(__MODULE__, shopping_list)
  end

  def init(shopping_list) do
    state = %{
      shopping_list: shopping_list
      inventory: []
    }
    {:ok, state, {:continue, :get_inventory}}
  end

  def handle_continue(:get_inventory, %{shopping_list: shopping_list}) do
    inventory = get_inventory_for(shopping_list)
    state = %{
      shopping_list: shopping_list,
      inventory: inventory
    }
    {:noreply, state}
  end

  defp get_inventory_for(shopping_list) do
    # something that could be time consuming!
    # like a web request or a database call
    # returns some inventory info for each item on the shopping list
  end
end 
```

现在，`init/2`返回`{:ok, state, {:continue, :get_inventory}}`。这立即引发了回调`handle_continue(:get_inventory, state)`。这个回调保证在我们的 GenServer 继续处理任何其他消息之前完成运行。

## 结论

OTP 21 的`handle_continue/2`回调允许我们以非阻塞、异步的方式处理昂贵的 GenServer 初始化工作，避免竞争情况。如果您正在构建一个需要处理初始化回调的 GenServer，可以考虑使用`handle_continue/2`。